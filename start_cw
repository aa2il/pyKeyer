#! /usr/bin/tcsh
################################################################################
#
# start_cw - Rev 1.1
# Copyright (C) 2021-5 by Joseph B. Attili, joe DOT aa2il AT gmail DOT com
#
# Script to startup everything for a CW contest.
#
# Notes:
#  1) set HINTS="-use_log_hist"
#     or otherwise dupe checking will not work if we restart
#     Need to rethink this scheme, perhaps put a time limit on it?
#
#  2) Can have pyKeyer talk directly to rig and SDR as a client but
#     this seems to be problematic for now. Instead, using FLRIG or HAMLIB 
#     seems more stable.
#
#  3) How to combine old capture & sidetone wave files:
#     sox capture_20200625_183249.wav sidetone_20200625_183249.wav \
#                              --channels 2 --combine merge stereo.ogg
#     ... and to play it:
#     audacity stereo.ogg
#
################################################################################
#
# Usual way to run this is to with FLRIG controling the radio & the keyer and
# SDR working through it.
#
#     RIG <----------------> FLRIG
#             USB Port       ^   ^  
#                            |   | Both via 
#                            |   | xlmrpc 12345
#                            |   |
#                            |   |         
#                 pyKeyer ---+   +---- SDR 
#
###############################################################################

# Turn this on to use the development settings
set DEVELOPMENT    = 0       # 0

# Select codes to start
set START_SDR      = 1       # 1
set START_CLOCK    = 1       # 1
set START_BANDMAP  = 1       # 1
set USE_NEW_LOGGER = 0       # 0 - still experimental

if( $USE_NEW_LOGGER  )then
    set START_BANDMAP = 0
    set KEYER = "pyLogger"
else
    set KEYER = "pyKeyer"
endif

# Select various processing options
set SO2V          = 0       # 0
set USE_NANO_IO   = 1       # 1
set TEST_MODE     = 0       # 0
set KILL_OLD      = 1       # 1

# Select a contest
set CONTEST=""
#set CONTEST="ragchew"
#set CONTEST="cwt"
#set CONTEST="sst"
#set CONTEST="sstopen"
#set CONTEST="mst"
#set CONTEST="sprint"
#set CONTEST="state"

#set CONTEST="naqp"
#set CONTEST="arrl_dx"                # Both CW and SSB versions
#set CONTEST="cq160m"
#set CONTEST="foc"
#set CONTEST="cqmm"
#set CONTEST="aa"
#set CONTEST="wpx"
#set CONTEST="rac"
#set CONTEST="marconi"
#set CONTEST="vhf"
#set CONTEST="wfd"
#set CONTEST="fd"
#set CONTEST="iaru"
#set CONTEST="cqvhf"
#set CONTEST="iota"
#set CONTEST="marac"
#set CONTEST="cwopen"
#set CONTEST="cqp"
#set CONTEST="ss"
#set CONTEST="cqww"
#set CONTEST="ten"
#set CONTEST="stew"
#set CONTEST="skcc"

# Specify one or more state QSO parties - W1=New England QP, W7=7QP
set STATE="VT MN BC"

# Various options for pyKeyer
set CAPTURE=""
set CAPTURE="-capture"
#set CAPTURE="-capture -sidetone"
set MODE="-mode CW"

set PRACTICE=""
set HINTS=""

set UNASSISTED="-cluster NONE"
set UNASSISTED=""

set BM_SERVER="-cluster W3LPL"
#set BM_SERVER=""

set BAUD = 38400
set PORT = 0
set PORT2 = 0

set UDP=""
set UDP="-udp"
set BM_UDP="-bm_udp"

set SCP=""
#set SCP="-scp"

# If no contest was selected, default to CWT or SST
set d=`date -u +"%a %H"`
if( X$CONTEST == "X" )then
    set day  = $d[1]
    set hour = $d[2]
    echo $day    $hour
    if( $day == "Mon" )then
        echo Hey 1
        if ( (($hour >= 12) && ($hour <= 13)) || (($hour >= 18) && ($hour <= 19)) )then
            set CONTEST="mst"
        else if( $hour < 1 )then
            set CONTEST="sst"
        endif
    else if( $day == "Tue" )then
        echo Hey 2
        if ( ($hour >= 2) && ($hour <= 3) )then
            set CONTEST="mst"
        endif
    else if( $day == "Wed" )then
        echo Hey 3
        if( (($hour >= 12) && ($hour <= 13)) || (($hour >= 18) && ($hour <= 19)) )then
            set CONTEST="cwt"
        endif
    else if( $day == "Thu" )then
        echo Hey 4
        if( (($hour >= 2) && ($hour <= 3)) || (($hour >= 6) && ($hour <= 7)) )then
            set CONTEST="cwt"
        endif
    else if( ($day == "Fri") && ($hour >= 1) && ($hour <= 2) )then
        echo Hey 5
        set CONTEST="sprint"
    else if( ($day == "Fri") && ($hour >= 19) && ($hour <= 20) )then
        echo Hey 6
        set CONTEST="sst"
    else if( ($day == "Sun") && ($hour >= 23) )then
        echo Hey 7
        set CONTEST="sst"
    endif
    echo Hey 99 - day=${day}   hour=${hour}
endif
if( X$CONTEST == "X" )then
        echo " "
        #echo "--- Can't figure out contest - defaulting to RAGCHEW ---"
        echo "--- Can't figure out contest - defaulting to DX ---"
        echo " "
        #set CONTEST="ragchew"
        set CONTEST="dx"
        #exit
endif
echo CONTEST=$CONTEST
#exit 0

# VHF Contest or VHF command center
if( 0 )then
    set START_SDR     = 0
    set START_CLOCK   = 0
    set START_BANDMAP = 1
    set CONTEST       = "vhf"
    set USE_NANO_IO   = 1      # 1
    set RIG_TYPE1="FT991a"
    #set RIG_TYPE1="IC9700"
    set CAPTURE=""
    set BM_UDP="-bm_udp -vhf"
endif

echo CONTEST=$CONTEST
#exit

# Which python interprettor to use
set PYTHON=""
#conda activate aa2il
#conda activate py3_13
#set PYTHON="python"
#set PYTHON="uv run"
which python

# Rig settings
set FIND_RIG="$PYTHON ~/Python/findRig/findRig.py"
set IF_SHIFT="0000"
set MON=5                      # Monitor level

# SDR Options
set SDR_DRIVER=""
#set SDR_DRIVER="-fake"

# Debug info
echo FIND_RIG=$FIND_RIG
echo IF_SHIFT=$IF_SHIFT
echo MON=$MON

cd ~/Python/pyKeyer
pwd
#exit

################################################################################

# Select primary rig & middleware
#set RIG1="FLDIGI"
set RIG1="FLRIG"
set RIG1="HAMLIB"
#set RIG1="DIRECT"

set RIG_TYPE1=""
#echo Calling FIND_RIG=$FIND_RIG ...
#set RIG_TYPE1=`$FIND_RIG`
#echo ... RIG_TYPE1=$RIG_TYPE1

#set RIG_TYPE1="FTdx3000"
#set RIG_TYPE1="FT991a"
#set RIG_TYPE1="TYT9000d"
#set RIG_TYPE1="IC9700"
#set RIG_TYPE1="TS850"
#set RIG_TYPE1="IC706"

#set RIG_TYPE2="IC9700"

# Select secondary rig & middleware
# Very limited for now
set RIG2="NONE"
#set RIG2="DIRECT"
#set RIG2="FLRIG"

set RIG_TYPE2="NONE"
#set RIG_TYPE2="TS850"
#set RIG_TYPE2="IC706"
#set RIG_TYPE2="FTdx3000"
#set RIG_TYPE2="FT991a"
#set RIG_TYPE2="IC9700"
#set RIG_TYPE2=`$FIND_RIG`

# CQ Analog VHF
if( X$CONTEST == "Xcqvhf" )then
    set START_SDR     = 0
    set START_CLOCK   = 1    # 1
    set START_BANDMAP = 1    # 1
    set USE_NANO_IO   = 1    # 0 - Gets Confused about 2nd rig so leave it off for now BUT keying timing is hosed!!!
    set CAPTURE=""
    set BM_UDP="-bm_udp -vhf"
    
    #set RIG1="DIRECT"               # Ok for IC9700
    set RIG1="HAMLIB"               # Ok for IC9700
    #set RIG1="FLRIG"
    set RIG_TYPE1="FT991a"
    #set RIG_TYPE1="IC9700"

    #set RIG2="DIRECT"
    set RIG2="HAMLIB"               # 
    set RIG_TYPE2="IC9700"
endif

# Testing/development 
if( $DEVELOPMENT )then
    set START_SDR     = 0
    set START_CLOCK   = 0
    set START_BANDMAP = 0
    set RIG1="DIRECT"
    #set RIG1="HAMLIB"
    #set RIG1="FLRIG"
    #set RIG1="FLDIGI"
    set RIG_TYPE1="IC9700"

    set CONTEST       = "vhf"
    set USE_NANO_IO   = 0
    set RIG_TYPE1="FTdx3000"
    #set RIG_TYPE1="FT991a"
    #set RIG_TYPE1="IC9700"
    #set RIG2="DIRECT"
    #set RIG2="DUMMY"
    #set RIG_TYPE2="TYT9000d"
    set CAPTURE=""
    #set BM_UDP="-bm_udp -vhf"
endif

set BM_OPTS = "$BM_UDP -show_year $UNASSISTED -modes CW -save"           # Show DCXXs needed for this year

set SDR_OPTS =  ""
set SDR_OPTS =  "-delay 32"               # Let's see if this helps stuttering problem

################################################################################

# Force unbuffered output to stdout - same as python -u opt
setenv PYTHONUNBUFFERED 1

# Kill stale stuff
echo " "
echo "Cleaning up old junk ..."
if( $KILL_OLD )then
    pkill pyKeyer
    pkill pyLogger
    pkill miniterm               # This might be connected to NanoIO
    pkill rigctl
    pkill rigctld
    pkill wsjt
    pkill wsjtx
    pkill bandmap
    pkill flrig
    pkill wclock
    pkill bandmap
    pkill oscope

    # Kill the sandboxed progs also
    kill_prog wclock.py
    kill_prog bandmap.py
    kill_prog pyKeyer.py
    kill_prog pyLogger.py
    
    rm -f /tmp/HAMLIB /tmp/WCLOCK /tmp/KEYER
    rm -f /tmp/FLDIGI* /tmp/FLRIG*
    ls /tmp
    rm -f ~/Python/LOGS/*MEMORY*.TXT ~/Python/LOGS/KEYER
    mkdir ~/Python/LOGS
    mv /tmp/SDR* /tmp/KEYER* /tmp/BANDMAP* /tmp/RIGCTL* ~/Python/LOGS
    sleep 1
    #exit
endif

################################################################################

# Determine primary rig type
echo RIG_TYPE1a=-$RIG_TYPE1-$#RIG_TYPE1-
if( $RIG_TYPE1 == "" || $RIG_TYPE1 == "None" )then
    sleep 1
    set RIG_TYPE1=`$FIND_RIG`
    echo RIG_TYPE1b=-$RIG_TYPE1-$#RIG_TYPE1-
endif
if( $RIG_TYPE1 == "" || $RIG_TYPE1 == "None" )then
    sleep 1
    set RIG_TYPE1=`$FIND_RIG`
    echo RIG_TYPE1c=-$RIG_TYPE1-
endif
set RIG_TYPE1=`echo $RIG_TYPE1 | cut -f 1 -d ' '`
echo RIG_TYPE1=-$RIG_TYPE1-
if( $RIG_TYPE1 == "" || $RIG_TYPE1 == "None" )then
    echo " "
    echo "--- Well thats embarassing - cant find rig - make sure its plugged in --- "
    echo " "
    exit
endif
#exit

################################################################################

# Determine current workspace number - we'll use this later to
# keep windows on this workspace instead of wandering
echo " "
set WS=`wmctrl -d | fgrep " * " | cut -f 1 -d ' '`
echo Current Workspace = $WS
echo " "
if( $USE_NEW_LOGGER  )then
    set BM_OPTS = "$BM_OPTS -bm_geo 400x790+1520+240"
else
    set BM_OPTS = "$BM_OPTS -bm_geo 400x790+1520+240 -desktop $WS"
endif

#########################################################################

# Settings for NAQP CW contests
if( $CONTEST == "naqp" )then
    set WPM=30
    set HINTS="-use_log_hist -autofill"
    #set BM_OPTS = "$BM_OPTS -contest -hours 12 -cwops -nodupes -na_only"
    set BM_OPTS = "$BM_OPTS -contest $CONTEST -hours 12 -cwops -nodupes -na_only"
    #set SDR_OPTS =  "$SDR_OPTS -delay 32"
    set SCP="-scp"

# Settings for CW Ops mini tests
else if( $CONTEST == "cwt" )then
    set WPM=32
    set HINTS="-autofill"
    #set BM_OPTS = "$BM_OPTS -contest $CONTEST -hours 1 -cwops -nodupes"
    set BM_OPTS = "$BM_OPTS -contest -hours 1 -cwops -nodupes"
    set SCP="-scp"

# Settings for CW Ops cw open
else if( $CONTEST == "cwopen" )then
    set WPM=30
    set HINTS="-use_log_hist -autofill"
    set BM_OPTS = "$BM_OPTS -contest -hours 4  -cwops -nodupes"
    set SCP="-scp"

# Settings for Slow Speed Mini Test
else if( ($CONTEST == "sst") || ($CONTEST == "sstopen") )then
    if( $CONTEST == "sstopen" )then
        set BM_OPTS = "$BM_OPTS -contest $CONTEST -hours 4 -cwops -nodupes"
    else
        set BM_OPTS = "$BM_OPTS -contest $CONTEST -hours 1 -cwops -nodupes"
    endif
    set HINTS="-use_log_hist -autofill -nrows 1 -lock"
    set WPM=18
    set SCP="-scp"

# Settings for Medium Speed Mini Test
else if( $CONTEST == "mst" )then
    set HINTS="-use_log_hist -autofill -nrows 1"
    set WPM=23
    set BM_OPTS = "$BM_OPTS -contest -hours 1 -cwops -nodupes"
    set SCP="-scp"

# Settings for Ragchews
else if( $CONTEST == "ragchew" )then
    set HINTS="-use_log_hist -autofill -nrows 1"
    set BM_OPTS = "$BM_OPTS -cwops"
    set WPM=20

# Settings for FOC BW
else if( $CONTEST == "foc" )then
    set WPM=30
    set HINTS="-autofill"
    set BM_OPTS = "$BM_OPTS -contest -hours 24 -cwops -nodupes"
    set SCP="-scp"

# Settings for SKCC - FOR LOGGING ONLY!!
else if( $CONTEST == "skcc" )then
    set HINTS="-use_log_hist -split"
    set WPM=20
    set BM_OPTS = "$BM_OPTS -hours 2"

# Settings for NCCC CW Sprints
else if( $CONTEST == "sprint" )then
    set WPM=30
    set HINTS="-autofill"
    set SCP=""
    set BM_OPTS = "$BM_OPTS -contest -hours 1 -cwops"
    
# Setting for CQ WPX (or any contest that requires rst + serial number)
else if( $CONTEST == "wpx" )then
    set WPM=28
    set HINTS="-use_log_hist"
    set BM_OPTS = "$BM_OPTS -contest -cwops -nodupes"
    set SDR_OPTS =  "$SDR_OPTS -delay 32"
    set SCP="-scp"

# Setting for CQ World Wide
else if( $CONTEST == "cqww" )then
    # Hints and log hist
    set WPM=28
    set HINTS="-use_log_hist -autofill -nrows 2"
    set BM_OPTS = "$BM_OPTS -contest -dx_only -cwops -nodupes"

# Setting for ARRL DX
else if( $CONTEST == "arrl_dx" )then
    # Hints and log hist
    set WPM=28
    set HINTS="-use_log_hist -nrows 2"
    set BM_OPTS = "$BM_OPTS -contest -dx_only -cwops -nodupes"
    set SCP="-scp"

# Settings for field day
else if( $CONTEST == "fd" || $CONTEST == "wfd" )then
    set WPM=25
    set HINTS="-use_log_hist -autofill"
    set BM_OPTS = "$BM_OPTS -contest -hours 30 -cwops -nodupes"
    set SDR_OPTS =  "$SDR_OPTS -delay 32"
    set SCP="-scp"

# Settings for IARU HF Champs
else if( $CONTEST == "iaru" )then
    set WPM=30
    set HINTS="-use_log_hist -autofill"
    set BM_OPTS = "$BM_OPTS -contest -hours 24 -cwops -nodupes"
    set SCP="-scp"

# Settings for CA QSO Party
else if( $CONTEST == "cqp" )then
    set WPM=28
    set HINTS="-use_log_hist -autofill -nrows 1"
    set BM_OPTS = "$BM_OPTS -contest $CONTEST -hours 30 -cwops -nodupes"
    set SCP="-scp"

# Settings for various state QPs
else if( $CONTEST == "state" )then
    set CONTEST="state $STATE"
    set HINTS="-use_log_hist -autofill -nrows 1 -max_age 48"
    set WPM=25
    set BM_OPTS = "$BM_OPTS -hours 48 -cwops"
    set SCP="-scp"

# Settings for ARRL & CQ VHF
else if( $CONTEST == "vhf" || $CONTEST == "cqvhf" )then
    set WPM=25
    set HINTS="-use_log_hist -autofill"
    set BM_OPTS = "$BM_OPTS -hours 48 -cwops"
    set SCP="-scp"

# Settings for ARRL 10m
else if( $CONTEST == "ten" )then
    set WPM=28
    set HINTS="-use_log_hist -autofill -nrows 2"
    set BM_OPTS = "$BM_OPTS -contest ARRL-10 -hours 48 -cwops -nodupes"
    set SCP="-scp"

# Settings for RAC Winter contest
else if( $CONTEST == "rac" )then
    set WPM=28
    set HINTS="-use_log_hist -autofill -nrows 1"
    set BM_OPTS = "$BM_OPTS -contest -hours 24 -cwops -nodupes"
    set SCP="-scp"

# Settings for Stew Perry 160m
else if( $CONTEST == "stew" )then
    set WPM=25
    set HINTS="-use_log_hist -autofill -nrows 1"
    set BM_OPTS = "$BM_OPTS -contest -hours 24 -cwops"

# Settings for CQ 160m
else if( $CONTEST == "cq160m" )then
    set WPM=25
    set HINTS="-use_log_hist -autofill -nrows 1"
    set BM_OPTS = "$BM_OPTS -contest -hours 48 -cwops"

# Settings for ARRL Sweepstakes
# Since we can only work each station once, it doesn't make sense to use history
# but we need it if we restart    
else if( $CONTEST == "ss" )then
    set WPM=28
    #set HINTS="-use_log_hist -autofill -max_age 30 -nrows 1"
    set HINTS="-use_log_hist -autofill -nrows 1"
    set BM_OPTS = "$BM_OPTS -contest $CONTEST -cwops -ss -nodupes -na_only -hours 30"

# Reasonable defaults
else
    #echo Unknown Contest:- $CONTEST-
    #exit
    set WPM=25
    set HINTS="-use_log_hist"
    set BM_OPTS = "$BM_OPTS -cwops"
    #set HINTS=""

endif
#exit

###############################################################################

# Not sure what this is all about?
# Probably something to do with start hamlib server in pyKeyer?
if( $RIG1 == "DIRECT" & $START_SDR )then
    set SERVER="-server"
else
    set SERVER=""
endif

################################################################################

# Start FLRIG
if( $RIG1 == "FLRIG" )then
    start_flrig $RIG_TYPE1
    #echo status=$status
    #set id=`pids flrig`
    #echo id=$id
    #if ( $#id == 0 ) then
    if ( $status != 0 ) then
        echo "--- ERROR --- Never started FLRIG - giving up"
        exit 1
    endif
    set PORT=12345                  # Port no. for FLRIG server
    #set RIG = "FLRIG"

    wmctrl -r flrig -t $WS
endif 
if( $RIG1 == "FLRIG" && $RIG_TYPE1 == "IC9700" )then
    set PORT=12346                  # Port no. for FLRIG server
endif 
if( $RIG2 == "FLRIG" )then
    start_flrig $RIG_TYPE2
    set PORT2=12346                  # Port no. for 2nd FLRIG server
endif
#exit

################################################################################

# Start hamlib server
if( $TEST_MODE == "1" )then
    set TEST_MODE="-test"
endif
if( $RIG1 == "HAMLIB" )then
    set PORT = 4532
    start_hamlib $RIG_TYPE1 $TEST_MODE -port $PORT
endif
if( $RIG2 == "HAMLIB" )then
    set PORT2 = 4534
    start_hamlib $RIG_TYPE2 $TEST_MODE -port $PORT2 -nokill
endif
#psu | fgrep rigctrl
#exit

################################################################################

# Start FLDIGI
if( $RIG1 == "FLDIGI" )then
    start_fldigi $RIG_TYPE1 
    #exit
endif

################################################################################

# Set basic rig configuration
# This capability is now in findRig
echo " "
echo Setting basic rig config ...
echo Setting CW Mode, Narrow Filtering and Power on port $PORT ...

set CMD="$FIND_RIG -rig $RIG1 $RIG_TYPE1 -port $PORT -M CW -FILT Narrow -PWR 99 -MON $MON -TUNER 1 -BREAK 1 -IFSHIFT 0 -ANT 1"
if( ($RIG_TYPE1 == "FTdx3000") && ($RIG1 != "HAMLIB") )then

    # Cancel any IF SHIFT 
    #set CMD="$CMD -w 'IS0+$IF_SHIFT'"

    # Select antenna, break-in and turn off lock
    #set CMD="$CMD -w 'AN01;BI1;LK4'"

    # Make sure these get propagated to VFO-B also
    #set CMD="$CMD -w 'SV;AN01;BI1;SV'"
    set CMD="$CMD -A2B"
    
endif

echo CMD=$CMD
$CMD
#exit

################################################################################

# Interface-dependent stuff
sleep 1
if( $USE_NANO_IO )then
    set NANO="-nano"
    #set NANO="-k3ng"
    set NANO="-winkeyer"
    #set NANO="-keyer ANY"
    #set NANO="-keyer NONE"
    set INTERNAL="w EX0190"
else
    #set NANO=" "
    set NANO="-keyer NONE"
    set INTERNAL="w EX0192"
endif

################################################################################

# Start the SDR
set LOOPBACK="-follow_freq"
set LOOPBACK="-follow_freq -bandmap"
#set LOOPBACK="-follow_freq -transpose -bandmap"
set SDR_OPTS="$SDR_OPTS -af_bw .5 $LOOPBACK $UDP -lna 4 -vol 10 -geo 1100x530+5+540"

# Do this for low-latency but can cause underflow problem - need to
# work on SDR a little more
#set SDR_OPTS="-af_bw .5 $LOOPBACK $UDP -vol 10 -geo 1100x530+5+540 -delay 4"

if( $SO2V )then
    #set SDR_OPTS=
else
    set SDR_OPTS="$SDR_OPTS -mute"
endif
if( $START_SDR )then
    echo Starting SDR ...
    pkill pySDR
    kill_prog pySDR.py
    cd ~/Python/pySDR
    #start_loopback

    # Add a delay to give hamlib server in the keyer time to come up
    #sleep 5
    echo Waiting for flrig/hamlib server to come alive ...
    echo f | nc -w 1 localhost $PORT

    set PY_SDR = "$PYTHON ~/Python/pySDR/pySDR.py"
    
    set FS=48
    set PAN="-pan -pan_bw 10 -pan_dr 90"
    #set PAN="-pan_bw 10 -pan_dr 90"

    set cmd="$PY_SDR $SDR_DRIVER -mode CW -fsout $FS -vid_bw 45 -ant B -rig $RIG1 -no_hamlib -port $PORT $SDR_OPTS $PAN -desktop $WS -minimized"
    set LOG="/tmp/SDR_`date -u +%y%m%d_%H%M%S`"
    echo " "   >& $LOG
    echo $cmd >>& $LOG
    echo " "  >>& $LOG
    date -u   >>& $LOG
    echo " "  >>& $LOG
    $cmd      >>& $LOG &
    sleep 1

    echo "Waiting for pySDR to start ..."
    set id=`find_windows pySDR 30`
    echo id=$id
    if ( $#id == 0 ) then
        echo "--- ERROR --- Never found pySDR after 30 tries - giving up"
        exit
    endif

    #wmctrl -r pySDR -t $WS
    #wmctrl -r "Demod Audio" -t $WS

    #exit
endif

################################################################################

# Use oscope to capture rig audio
if( X$CAPTURE != "X" )then
    #echo Starting Oscope ...
    #cd ~/Python/oscope
    #$PYTHON oscope.py >& /tmp/OSCOPE &
    #set CAPTURE=""
endif

################################################################################

ps -A u | fgrep -i rig

# This is here for code development purposes only
if( 0 )then
    echo "\n@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Starting pyRIG ..."
    pkill pyRig
    kill_prog pyRig.py
    sleep 1
    pushd ~/Python/pyRig
    echo $PYTHON pyRig.py -rig $RIG1 
    $PYTHON pyRig.py -rig $RIG1 >& /tmp/PYRIG &
    popd

    #$HAMLIB_DIR/rigctl -vvv -m $MODEL -s $BAUD -r /dev/$c
    tail -f /tmp/PYRIG
    exit
endif

# Start keyer
if( 1 )then
    echo Starting keyer ...
    cd ~/Python/${KEYER}

    set OPTS="-wpm ${WPM} $PRACTICE $CAPTURE -${CONTEST} -rig $RIG1 $RIG_TYPE1 -port $PORT $HINTS $SERVER $NANO $MODE $UDP -immediate $SCP -geo 1500x400+0+240 -desktop $WS"
    if( $RIG2 != "NONE" )then
        set OPTS="$OPTS -rig2 $RIG2 $RIG_TYPE2 -port2 $PORT2"
    endif
    if( $USE_NEW_LOGGER  )then
        set OPTS="$OPTS -bandmap $BM_OPTS"
    endif
    
    set CMD="$PYTHON ${KEYER}.py $OPTS"
    set LOG="/tmp/KEYER_`date -u +%y%m%d_%H%M%S`"
    echo " "   >& $LOG
    echo $CMD >>& $LOG
    echo " "  >>& $LOG
    date -u   >>& $LOG
    echo " "  >>& $LOG
    $CMD      >>& $LOG &
    #exit
    sleep 1

    echo "Waiting for ${KEYER} to start ..."
    #set id=`find_windows ${KEYER} 50`
    set id=`find_windows pyKeyer 50`
    echo id=$id
    if ( $#id == 0 ) then
        echo "--- ERROR --- Never found ${KEYER} after 30 tries - giving up"
        exit
    endif
    
    #wmctrl -r pyKeyer -t $WS
    
    #exit
endif

################################################################################

# Start World Clock
if( $START_CLOCK )then
    echo Starting World Clock ...
    pkill wclock
    kill_prog wclock.py
    sleep 1
    pushd ../wclock

    set CMD="$PYTHON wclock.py -geo 390x360+1110+710 -desktop $WS"
    set LOG="/tmp/WCLOCK"
    echo " "   >& $LOG
    echo $CMD >>& $LOG
    echo " "  >>& $LOG
    date -u   >>& $LOG
    echo " "  >>& $LOG
    $CMD      >>& $LOG &
    
    popd
    
    echo "Waiting for clock to start ..."
    set id=`find_windows "World Clock" 20`
    echo id=$id
    if ( $#id == 0 ) then
        echo "--- ERROR --- Never found wclock after 20 tries - giving up"
        exit
    endif

    #wmctrl -r "World Clock" -t $WS
    
endif

################################################################################

# Start Bandmap
if( $START_BANDMAP )then
    echo Starting Bandmap ...
    pkill bandmap
    kill_prog bandmap.py
    pushd ../bandmap
    #pushd ../bandmap/work
    #pushd ../bandmap/work2
    #pushd ../bandmap/work3
    set cmd="$PYTHON bandmap.py -rig $RIG1 -port $PORT $BM_OPTS"
    set LOG="/tmp/BANDMAP_`date -u +%y%m%d_%H%M%S`"
    echo " "   >& $LOG
    echo $cmd >>& $LOG
    echo " "  >>& $LOG
    date -u   >>& $LOG
    echo " "  >>& $LOG
    $cmd      >>& $LOG &
    popd

    echo "Waiting for Bandmap to start ..."
    set id=`find_windows "Band Map" 20`
    echo id=$id
    if ( $#id == 0 ) then
        echo "--- ERROR --- Never found bandmap after 20 tries - giving up"
        exit
    endif
    
    #wmctrl -r "Band Map" -t $WS
    
    #exit
    
endif
    
################################################################################

exit

sleep 1
organize_windows CW
echo Done.

wmctrl -l
